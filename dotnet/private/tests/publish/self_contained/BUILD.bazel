load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load(
    "//dotnet:defs.bzl",
    "publish_binary",
)

publish_binary(
    name = "self_contained",
    binary = "//dotnet/private/tests/publish/app_to_publish",
    runtime_packs = select({
        "//dotnet:rid_linux-x64": ["@paket.rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.linux-x64"],
        "//dotnet:rid_osx-x64": ["@paket.rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.osx-x64"],
        "//dotnet:rid_win-x64": ["@paket.rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.win-x64"],
        "//conditions:default": ["@paket.rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.linux-x64"],
    }),
    self_contained = True,
    target_framework = "net6.0",
)

# We tar the output to because that way we can strip the
# prefix of the files and avoid long path issues on Windows.
# This also makes sure that it's possible to tar the output
# of the publish without any problems
pkg_tar(
    name = "tar",
    srcs = [":self_contained"],
    include_runfiles = True,
    mode = "0755",
    package_dir = "/",
    strip_prefix = select(
        {
            "@platforms//os:linux": "self_contained/publish/linux-x64",
            "@platforms//os:osx": "self_contained/publish/osx-x64",
            "@platforms//os:windows": "self_contained/publish/win-x64",
        },
    ),
    # Disabling this for Windows because there are some runfiles issues with Windows and rules_pkg
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "@platforms//os:linux": [],
        "@platforms//os:macos": [],
    }),
)

sh_test(
    name = "self_contained_test",
    srcs = [
        "test.sh",
    ],
    data = [":tar"],
    # Disabling this for Windows because there are some runfiles issues with Windows and rules_pkg
    target_compatible_with = select({
        "@platforms//os:windows": ["@platforms//:incompatible"],
        "@platforms//os:linux": [],
        "@platforms//os:macos": [],
    }),
)
