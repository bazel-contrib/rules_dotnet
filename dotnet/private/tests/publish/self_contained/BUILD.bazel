load(
    "@rules_dotnet//dotnet:defs.bzl",
    "publish_binary",
)
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_pkg//pkg:zip.bzl", "pkg_zip")

publish_binary(
    name = "self_contained",
    binary = "//dotnet/private/tests/publish/app_to_publish",
    runtime_pack = select({
        "@rules_dotnet//dotnet:rid_linux-x64": "@rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.linux-x64",
        "@rules_dotnet//dotnet:rid_osx-x64": "@rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.osx-x64",
        "@rules_dotnet//dotnet:rid_win-x64": "@rules_dotnet_dev_nuget_packages//microsoft.netcore.app.runtime.win-x64",
    }),
    self_contained = True,
    target_framework = "net6.0",
)

# We tar the output to because that way we can strip the
# prefix of the files and avoid long path issues on Windows.
# This also makes sure that it's possible to tar the output
# of the publish without any problems
pkg_tar(
    name = "tar",
    srcs = [":self_contained"],
    mode = "0755",
    package_dir = "/",
    strip_prefix = select(
        {
            "@platforms//os:linux": "self_contained/publish/linux-x64",
            "@platforms//os:osx": "self_contained/publish/osx-x64",
            "@platforms//os:windows": "self_contained/publish/win-x64",
        },
    ),
    target_compatible_with = select({
        "@platforms//os:osx": [],
        "@platforms//os:linux": [],
        "//conditions:default": ["@platforms//:incompatible"],
    }),
)

# There are some issues with pkg_tar and windows so
# we zip the output instead
pkg_zip(
    name = "zip",
    srcs = [":self_contained"],
    mode = "0755",
    package_dir = "/",
    strip_prefix = select(
        {
            "@platforms//os:linux": "self_contained/publish/linux-x64",
            "@platforms//os:osx": "self_contained/publish/osx-x64",
            "@platforms//os:windows": "self_contained/publish/win-x64",
        },
    ),
)

sh_test(
    name = "self_contained_test",
    srcs = [
        "test.sh",
    ],
    data = select({
        "@platforms//os:linux": [
            ":tar",
        ],
        "@platforms//os:osx": [
            ":tar",
        ],
        "@platforms//os:windows": [
            ":zip",
        ],
    }),
)
